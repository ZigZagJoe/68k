Name     REV4-B-68008-Glue;
PartNo   00;
Date     6/17/2014;
Revision 4b;
Designer zigzagjoe;
Company  ;
Assembly ;
Location ;
Device   g22v10;

$define ~ !
$define | #

/* input pins */
PIN    1 = DS                         ; /* /DS from 68008                  */ 
PIN    2 = AS                         ; /* /AS from 68008                  */ 
PIN    3 = RW                         ; /* R/W- from 68008                 */ 
PIN    4 = A19                        ; /* Address bus bit 19              */ 
PIN    5 = A18                        ; /* Address bus bit 18              */ 
PIN    6 = A2                         ; /* Address bus bit 2               */ 
PIN    7 = FC0                        ; /* Processor status 1              */ 
PIN    8 = FC1                        ; /* Processor status 2              */ 
PIN    9 = FC2                        ; /* Processor status 3              */ 
PIN    10 = BOOT_RESET                ; /* /RESET from TL7705 (for BOOTED) */ 
PIN    11 = DTACKIN                   ; /* DTACK input, 68901 and others'  */ 
/* PIN 12 GROUND */
PIN    13 = SLOW_DTACK_T              ; /* delay signal, from 74LS164 SR   */

/* output pins */
PIN    14 = RD						  ; /* /RD strobe (memory, io devices) */ 
PIN    15 = WR                        ; /* /WR strobe (memory, io devices) */ 
PIN    16 = ROMCS                     ; /* CS to FLASH ROM                 */ 
PIN    17 = RAMCS                     ; /* CS to RAM                       */ 
PIN    18 = IOCS                      ; /* CS to 74LS138 IO decoder        */ 
PIN    19 = IACK                      ; /* IACK to 68901 MFP               */ 
PIN    20 = DTACK                     ; /* DTACK out to 68008              */                             
PIN    21 = BOOTED                    ; /* BOOTED (internal use only)      */
PIN    22 = SLOW_DTACK_CS1            ; /* /CS1 INPUT for a slow device    */
PIN    23 = SLOW_DTACK_CS2            ; /* /CS2 INPUT for a slow device    */
/* PIN 24 VCC 	*/

/* ############## EQUATIONS ############## */

/* ##### BOOTED flip flop ##### */
/* /BOOT_SET to set ff, /BOOT_R ESETto reset, and BOOTED is output */
/* BOOTED is not used outside of the GAL, but you can't 
   have a recursive equation without an output pin's
   feedback path back into the input logic matrix
   
   This generates the BOOTED signal, which is used for having the ROM respond
   to the 68008 reading the initial SP and PC from addresses 0 and 4. As the 
   initial PC will always be located in flash, a read using address line 19
   will always be generated, and thus will suffice as a trigger for setting 
   the BOOTED flag, until the next external reset by the TL7705.   */
   
BOOT_SET = ~(A19 & ~AS);
BOOTED   = ~(BOOT_SET   & BOOTED_I);
BOOTED_I = ~(BOOT_RESET & BOOTED);

/* ##### Interrupt ACKnowledge for 68901 ##### */
/* FC0-2 all being low with AS asserted indicates interrupt being services
   The 68000/8 will have emitted the interrupt number on address lines A1-3
   As the 68901 is always going to be A2, just check that. This could easily
   be expanded to support a second exclusive interrupt like this, just take A3
   as an input for IRQ5_IACK and connect its /IRQ to IPL0/2 - irq 5 when asserted */
   
IACK = AS | ~(FC0 & FC1 & FC2 & A2);

/* ##### read/write strobes ##### */
/* these are read/write strobes for intel-bus devices and memory */

RD = DS | ~RW;
WR = DS | RW;

/* ##### CS decoding ##### */
/* categorizes accesses into either RAM, ROM, or IO accesses, taking into account 
   the BOOTED flag which makes the ROM respond to addresses 0-8 on initial boot */
   
IOCS = AS | ~(A19 & A18);
RAMCS = AS | A19 | ~BOOTED;
ROMCS = AS | (BOOTED & ~A19) | (A19 & A18);

/* ##### DTACK generation ##### */

/* SLOW_DTACK is a sub-signal to all the usage of extremely slow IO devices
   Connect the device's /CS input to SLOW_DTACK_CS1 or 2 and an output from the 
   shift register (that isn't the last one, used for BERR) to SLOW_DTACK_T 
   to generate an extremely slow (in computer terms) DTACK pulse. Two devices
   may be connected at once, however, they will share the same DTACK delay. */
   
SLOW_DTACK = (SLOW_DTACK_CS1 & SLOW_DTACK_CS2) | ~SLOW_DTACK_T;

/* RAM and ROM have no wait states */
DTACK = DS | (RAMCS & ROMCS & DTACKIN & SLOW_DTACK);

